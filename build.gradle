plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    id 'idea'
}

group 'online.xloypaypa'
version '1.0.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenCentral()
    mavenLocal()
}

def grpcVersion = '1.24.0'
def nettyTcNativeVersion = '2.0.26.Final'
def protocVersion = '3.10.0'

jar {
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'online.xloypaypa.dns.relay.Main'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

dependencies {
    implementation group: 'org.clojure', name: 'clojure', version: '1.10.1'
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compileOnly "javax.annotation:javax.annotation-api:1.2"
    runtime "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
            srcDir 'src/main/clojure'
        }
    }
}
